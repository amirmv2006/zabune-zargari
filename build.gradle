import de.itemis.mps.gradle.BuildLanguages
import de.itemis.mps.gradle.RunAntScript

buildscript {
    repositories {
        maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }
        mavenCentral()
    }
    dependencies {
        classpath 'de.itemis.mps:mps-gradle-plugin:1.2+'
        classpath 'de.undercouch:gradle-download-task:4.0.4'
    }
}


apply plugin: 'de.undercouch.download'
apply plugin: 'maven-publish'

group = 'ir.amv.laas.samples'
version = '0.0.1-SNAPSHOT'
ext.mpsMajor = '2020.2'
ext.mpsMinor = '2'
ext.jbrSdkVersion = '11_0_8'
ext.jbrBuild = 'b1129.2'

allprojects {
    repositories {
        mavenCentral()
    }
}
configurations {
    mps
    ant_lib
}

repositories {
    maven { url 'https://projects.itemis.de/nexus/content/repositories/mbeddr' }

    ivy {
        url "https://download.jetbrains.com/mps/$mpsMajor/"
        layout 'pattern', {
            artifact "[module]-[revision].[ext]"
        }
        metadataSources { // skip downloading ivy.xml
            artifact()
        }
    }

}

dependencies {
    mps "com.jetbrains:MPS:$mpsMajor.$mpsMinor@zip"
    ant_lib "org.apache.ant:ant-junit:1.10.1"
}
ext["itemis.mps.gradle.ant.defaultScriptClasspath"] = configurations.ant_lib.fileCollection { true }
ext["itemis.mps.gradle.ant.defaultScriptArgs"] = ["-Dbasedir=."]
publishing {
    publications {
        maven(MavenPublication) {
            artifact("build/artifacts/zargari_Plugin/ir.amv.laas.samples.zargari.lang.zip") {
                extension 'zip'
            }
        }
    }
    repositories {
        maven {
            name 'nexus'
            url repoUrl
            credentials {
                username project.repoUser
                password project.repoPassword
            }
        }
    }
}

task downloadOSXJBRZip(type: Download, dependsOn: []) {
    src "https://jetbrains.bintray.com/intellij-jbr/jbrsdk-$jbrSdkVersion-osx-x64-${jbrBuild}.tar.gz"
    dest file('lib/jbr.osx.tar.gz')
    overwrite false
}

task downloadOSXJBR(type: Copy, dependsOn: [downloadOSXJBRZip]) {
    from tarTree('lib/jbr.osx.tar.gz')
    into file('lib/jbr-osx')
}

task downloadLinuxJBRZip(type: Download, dependsOn: []) {
    src "https://jetbrains.bintray.com/intellij-jbr/jbrsdk-$jbrSdkVersion-linux-x64-${jbrBuild}.tar.gz"
    dest file('lib/jbr.linux.tar.gz')
    overwrite false
}

task downloadLinuxJBR(type: Copy, dependsOn: [downloadLinuxJBRZip]) {
    from tarTree('lib/jbr.linux.tar.gz')
    into file('lib/jbr-linux')
}

task downloadWindowsJBRZip(type: Download, dependsOn: []) {
    src "https://jetbrains.bintray.com/intellij-jbr/jbrsdk-$jbrSdkVersion-windows-x64-${jbrBuild}.tar.gz"
    dest file('lib/jbr.windows.tar.gz')
    overwrite false
}

task downloadWindowsJBR(type: Copy, dependsOn: [downloadWindowsJBRZip]) {
    from tarTree('lib/jbr.windows.tar.gz')
    into file('lib/jbr-windows')
}

task downloadDependencies(type: Copy, dependsOn: []) {
    dependsOn configurations.mps
    into 'lib'
    configurations.mps.files.each {
        from zipTree(it)
    }
}

task prepare(dependsOn: [downloadDependencies]) {}

task buildBootstrap(type: BuildLanguages, dependsOn: [prepare]) {
    script 'build-bootstrap.xml'
}

task generate(type: RunAntScript, dependsOn: [buildBootstrap]) {
    script 'build/build-plugin.xml'
    targets 'generate'
}

task buildPlugin(type: BuildLanguages, dependsOn: [buildBootstrap]) {
    script 'build/build-plugin.xml'
}

task copyStartupScripts(type: Copy, dependsOn: [buildBootstrap]) {
    from 'code/buildscripts/solutions/ir.amv.snippets.cats.ide.build/source_gen/ir/amv/snippets/cats/ide/build'
    include '*.bat'
    include '*.sh'
    include '*.vmoptions'
    include 'Info.plist.xml'
    into 'build/startup'
}

task $buildRcp(type: BuildLanguages, dependsOn: [buildPlugin]) {
    script 'build/build-rcp.xml'
}

task buildRcp(type: RunAntScript, dependsOn: [$buildRcp, copyStartupScripts]) {
    script 'build/build-rcpdistrib.xml'
    targets 'clean', 'build'
}

task buildRcpWithJBR(type: RunAntScript, dependsOn: [$buildRcp, copyStartupScripts, downloadOSXJBR, downloadLinuxJBR, downloadWindowsJBR]) {
    script 'build/build-rcpdistrib-jbr.xml'
    targets 'clean', 'build'
}

task clean(type: Delete, dependsOn: []) {
    delete 'build'
    delete 'lib'
    delete fileTree('.').matching {
        include '**/source_gen/**'
        include '**/source_gen.caches/**'
        include '**/classes_gen/**'
    }
}
